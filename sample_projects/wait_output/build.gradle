apply plugin: 'java'
apply plugin: 'gradle-execfork-plugin'

buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
  }
  dependencies {
    classpath "com.github.psxpaul:gradle-execfork-plugin:$pluginVersion"
  }
}

task integrationTest(dependsOn: 'startDaemon') {
  doLast {
    def content = file("${buildDir}/daemon.out").text
    assert content.contains('Important work being done!')
    assert content.contains('Daemon is up!')
    assert !content.contains('PING')
    Thread.sleep(1500)
    content = file("${buildDir}/daemon.out").text
    assert content.contains('PING')
  }
}
build.dependsOn integrationTest

task startDaemon(type: com.github.psxpaul.task.JavaExecFork, dependsOn: 'classes') {
  classpath = sourceSets.main.runtimeClasspath
  main = 'com.github.psxpaul.example.Main'
  standardOutput = "${buildDir}/daemon.out"
  waitForOutput = 'Daemon is up!'
}
